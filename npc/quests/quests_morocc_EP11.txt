//===== rAthena Script ======================================= 
//= Quest NPCs related to Morroc
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 3.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= - Stop Post
//= - Binoculars Quest
//= - Assassin Pub NPCs/supporting quest NPCS. [Disabled]
//= - Resurrection of Satan Morroc
//= - The Crow of the Fate
//= - Succession of the Prince
//===== Additional Comments: ================================= 
//= 1.1 Fixed exploits [Lupus]
//= 1.2 Added Dandelion's Request / Morroc Invasion quest. [SinSloth]
//= 1.3 Updated dialogs from Binoculars Quest. [Samuray22]
//= 1.3a added GM lvl check as in other invansion quests [Lupus]
//= 1.4 Fixed a small bug in Dandelion's Request. [SinSloth]
//= 1.5 Fixed a little bug with "Job_Alchemist" changed to "Job_Alchem". [Samuray22]
//= 1.6 Fixed a nasty bug in last part of the quest. [SinSloth]
//= 1.6a replaced item "names" with item id. [Lupus]
//= 1.7 Fixed a bug with XP rewards. [SinSloth]
//= 1.8 Fixed a bug which prevented reset of events. [SinSloth]
//= 1.9 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.0 Fixed bad NPC header data to comply with rev. 11603. [L0ne_W0lf]
//= 2.0a Corrected a Typo error ";;". [Samuray22]
//= 2.1 Assassins will now get their request item. (bugreport:1220) [L0ne_W0lf]
//= 2.2 Updated Binocular and Stop Post quests. [L0ne_W0lf]
//= 2.3 Replaced effect numerics with constants. [Samuray22]
//= 2.4 Implemented Ep. 12.1 Continental Guard Quest. [L0ne_W0lf]
//=     Moved Dandelion's Request to a it's own file.
//=     Added support NPCs required to finish Gaebolg quest.
//= 2.5 Fixed Group of Evil never re-enabling. [L0ne_W0lf]
//= 2.6 Added quest log commands for Resurrection of Satan Morroc. [L0ne_W0lf]
//=     Added the two remaining episode 12.1 quests. 
//=     Updated Satan Morroc Quest to allow peopel who have
//=     finished it to return to the previous map.
//= 2.7 Corrected weight checks, they only check for inventory slots. [L0ne_W0lf]
//= 2.8 Disabled Assassin bar pub NPCs, as they are included in the 13.1 quests file. [L0ne_W0lf]
//= 2.9 Fixed player dying/logging out during conversation could prevent
//=     summoning of Satan Morroc until server reboot. [Gepard]
//= 3.0 Updated RE/Pre-RE EXP. [Euphy]
//= 3.1 Updated to match the latest official scripts. [Euphy]
//= 3.2 Added GM management NPC and variable $@re_moc_time$. [Euphy]
//============================================================ 

// Stop Post Quest
// ============================================================
morocc_in,142,100,44	script	William	89,{
	mes "[William]";
	mes "Welcome to";
	mes "MacMillan's ^3355FFPost^000000 Workshop.";
	next;
	mes "[William]";
	mes "My family, the MacMillan Clan, has been producing Professional Traffic Signal Posts for more than 250 years. Nowadays, we are booking Special Orders for our unique ornament, ^3355FFStop Post^000000.";
	next;
	switch(select("Could I order one ^3355FFStop Post^000000?:Nah...")) {
	case 1:
		mes "[William]";
		mes "This unique ornament, ^3355FFStop Post^000000, is a traffic signal on the road, and doubles as a hair ornament! This, we solemnly promise on the strength of a 100-year guarantee.";
		next;
		mes "[William]";
		mes "To produce a ^3355FFStop Post^000000, we need ^3355FF91100 Zeny^000000, ^3355FF50 Trunk^000000 and ^3355FF1 Black Dyestuffs^000000.";
		next;
		if ((countitem(1019) > 49) && (countitem(983) > 0) && (Zeny > 91099)) {
			delitem 1019,50;  //Wooden_Block
			delitem 983,1;  //Black_Dyestuffs
			set Zeny, Zeny-91100;
			mes "[William]";
			mes "Here you are~!";
			mes "This ^3355FFStop Post^000000 has";
			mes "been especially made";
			mes "just for you!";
			mes "Thank you for stopping by!";
			mes "...Get the joke?";
			getitem 2272,1; // Stop_Post
			close;
		}
		else {
			mes "[William]";
			mes "So...";
			mes "When you get those items, swing on by and we'll give you a Stop Post.";
			close;
		}
	case 2:
		mes "[William]";
		mes "Anyway, thank you for coming by 'MacMillan's Workshop.' But think about buying something next time, will ya?";
		close;
	}
}

// Binoculars Quest
// ============================================================
morocc_in,76,163,4	script	Alchemist	64,{
	mes "[Marius]";
	mes "Howdy, new customer!!";
	mes "I know what you're gonna talk about. You want the Magic Glasses from me, riiiiight?";
	next;
	switch(select("What is that?:Manufacture:Ignore him")) {
	case 1:
		mes "[Marius]";
		mes "Hmm~!";
		mes "Well, the official name for them is ^3355FFBinoculars^000000! An optical device that works like a pair of field glasses, they're designed for simultaneous use by both eyes!";
		next;
		mes "[Marius]";
		mes "They're made up of two small telescopes joined with a single focusing device. You can arrange the lenses to produce stereoscopic vision.";
		next;
		mes "[Marius]";
		mes "So?";
		mes "Ain't that";
		mes "something, huh?";
		mes "Muhahahahahaha!";
		mes ". . . . .";
		next;
		mes "[Marius]";
		mes "Hm? You don't seem to";
		mes "believe what I just said?";
		mes "Oh c'mon, jerk. It's real!!";
		next;
		mes "[Marius]";
		mes "I will let you";
		mes "know the items I need...";
		mes "1 ^3355FFGeek Glasses^000000! 100 ^3355FFSteel^000000!";
		mes "And ^3355FF50000 Zeny^000000!";
		close;
	case 2:
		if ((countitem(2243) > 0) && (countitem(999) > 99) && (Zeny > 49999)) {
			mes "[Marius]";
			mes "Perfect, perfect !";
			mes "Now my masterpiece will be complete!";
			mes "Muhahahaha !";
			next;
			delitem 2243,1;  //Spinning_Eyes
			delitem 999,100;  //Steel
			set Zeny, Zeny-50000;
			mes "[Marius]";
			mes "Here you are!";
			mes "Binoculars !";
			getitem 2296,1; // Binoculars
			next;
			mes "[Marius]";
			mes "Just remember...";
			mes "Don't peep at something you shouldn't look at. Well, at least try not to.";
			close;
		}
		else {
			mes "[Marius]";
			mes "Argggghhhhhhh!!";
			mes "You didn't bring";
			mes "enough items!!!!";
			mes "How dare you";
			mes "disgrace me!!!";
			mes "Baaaadddd !!";
			close;
		}
	case 3:
		mes "[Marius]";
		mes "Hey you~!";
		mes "Now you're in front of Marius, an Alchemist among Alchemists. Ignoring me, eh? Come on, I'll beat your ass!";
		close;
	}
}

// Morroc Assassin Guild Bar [Disabled]
// ============================================================
morocc,43,108,5	script	Sharp-Looking Kid#dan_07	82,{
	if (BaseClass == Job_Assassin) {
		mes "[Jack]";
		mes "In spite of the chaotic mess in their town, ";
		mes "there are still some of those who pay visits here.";
		mes "Maybe the stronghold itself is a real one of a kind..";
		close;
	}
	else {
		if (countitem(7416) > 0) {
			mes "[Jack]";
			mes "A letter of recommendation?";
			mes "Oh, boy. Looks like you've brought me a very, very old relic from the past.";
			mes "Well, guess you should go in and see the master.";
			close;
		}
		else {
			mes "[Jack]";
			mes "Hey, what are you peeking at?";
			mes "Don't you have better things to do? You'd better go to the shelter and save your ass.";
			close;
		}
	}
}

morocc,45,110,0	script	que_job01#01	45,2,2,{
OnTouch:
	if (BaseClass == Job_Assassin) {
		warp "que_job01",9,94;
		end;
	}
	else {
		mes "[Jack]";
		mes "Wait, that's the restricted area....";
		mes "......";
		mes "Oh, well.";
		next;
		mes "[Jack]";
		mes "Things have changed.";
		mes "Just do whatever you want!";
		close2;
		warp "que_job01",9,94;
		end;
	}
}

que_job01,6,94,0	warp	morocc#01	2,2,morocc,45,103
que_job01,17,48,0	warp	que_job01#02	2,2,que_job01,68,92
que_job01,68,96,0	warp	que_job01#03	2,2,que_job01,17,53

que_job01,80,77,0	script	que_job01#04	45,2,2,{
OnTouch:
	if (BaseClass == Job_Assassin) {
		warp "que_job01",61,50;
		end;
	}
	else if (mao_request > 0) {
		warp "que_job01",61,50;
		end;
	}
	else {
		mes "[Litheron]";
		mes "Whoa, you're not";
		mes "allowed to be in here.";
		mes "Hey, master! Do you";
		mes "know this guy?";
		next;
		if (prt_curse == 24) {
			mes "[" + strcharinfo(0) + "]";
			mes "I don't care what you say, I'm coming in!!!";
			next;
			mes "[Litheron]";
			mes "Hey, master-! What do we do with this guy?";
			mes "He sure seems tough!";
			next;
			mes "[Master]";
			mes "Hmm. Let him do what he wants to do.";
			mes "Passing through one door doesn't make any difference.";
			next;
			mes "[Litheron]";
			mes "Hah! Did you hear that?";
			mes "Alright, how about this?";
			mes "I'll let you in if you buy me a drink.";
			mes "Besides, you can't risk making trouble here: this place";
			mes "is full of deadly Assassins.";
			next;
			switch(select("Sure:Why should I?!")) {
			case 1:
				if (Zeny > 999) {
					mes "[Litheron]";
					mes "Heh, that's what";
					mes "I'm talking about!";
					mes "Hey, bartender! Gimme";
					mes "the usual! I like your";
					mes "style, adventurer...";
					set Zeny, Zeny-1000;
					next;
					mes "[Litheron]";
					mes "Alright, you can come";
					mes "on in. But don't you dare";
					mes "breathe a word about this";
					mes "bar to another living soul.";
					close2;
					warp "que_job01",61,50;
					end;
				}
				else {
					mes "[Litheron]";
					mes "Huh...";
					mes "Oh, you don't even";
					mes "have enough zeny to";
					mes "buy water here. Oh boy...";
					mes "If you really want to enter,";
					mes "make sure you have the cash!";
					close2;
					warp "que_job01",85,77;
					end;
				}
			case 2:
				mes "[Litheron]";
				mes "Not the saavy type,";
				mes "are you...? Fine, fine.";
				mes "If you're not gonna do";
				mes "me any favors, then why";
				mes "should I help you? Go away!";
				close2;
				warp "que_job01",85,77;
				end;
			}
		}
		else {
			mes "[Master]";
			mes "Not at all.";
			mes "Make sure that this";
			mes "one isn't allowed";
			mes "to enter.";
			next;
			mes "[Litheron]";
			mes "You heard the man.";
			mes "You better retreat";
			mes "from this place while";
			mes "you still have legs...";
			close2;
			warp "que_job01",85,77;
			end;
		}
	}
}

que_job01,65,50,0	warp	que_job01#05	2,2,que_job01,84,77

que_job01,81,79,5	script	Idle Knight#dan_08	733,{
	mes "[Litheron]";
	mes "What's the matter?";
	mes "Oh, I see... You're";
	mes "probably wondering why";
	mes "a Knight like me is in a";
	mes "secret Assassin's pub.";
	if (BaseClass == Job_Knight) {
		mes "What about you, huh?";
	}
	next;
	mes "[Litheron]";
	mes "You know.. I just came to take a short breather. This whole restoration work is killing me.";
	mes "It's almost impossible to spot a place for a little drink these days, if you know what I mean.";
	close;
}

que_job01,82,95,3	script	Bar Master#moc_main01	46,{
	if (checkweight(908,200) == 0) {
		mes "^3355FFWait a second!";
		mes "Right now, you're carrying";
		mes "too many things with you.";
		mes "Please come back after";
		mes "using the Kafra Service";
		mes "to store some of your items.^000000";
		close;
	}
	if (BaseClass == Job_Assassin) {
		mes "[Master]";
		mes "Please, come in.";
		mes "Care for a drink?";
	}
	else if ((BaseJob == Job_Thief) && (BaseClass != Job_Assassin)) {
		mes "[Master]";
		mes "......Huh. How did a ruffian like you get in here?";
		mes "Well, I guess Jack's not doing his job.";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "Hey,master, don't be so harsh. We know each other, don't we?";
		mes "You don't wanna hurt your old friend's feeling~";
		next;
		mes "[Master]";
		mes "Whatever you say.";
		mes "So,what do you wanna drink?";
	}
	else {
		mes "[Master]";
		mes "....Hmm...";
		mes "What do you wanna drink?";
	}
	next;
	switch(select("Order a Drink:Ask About Mission:Cancel")) {
	case 1:
		mes "[Master]";
		mes "In this place, I only serve two drinks.";
		mes "You want a Tropical Sograt or";
		mes "a Vermilion on the Beach?";
		next;
		switch(select("Tropical Sograt:Vermilion on the Beach:Do you offer any free drinks?")) {
		case 1:
			if (checkweight(908,200) == 0) {
				mes "[Master]";
				mes "Hey, why did you bring";
				mes "so much stuff with you?";
				mes "I can't give you anything";
				mes "to drink if you don't have";
				mes "the room to carry it around.";
				mes "Clear out your inventory!";
				close;
			}
			else {
				if (BaseClass == Job_Assassin) {
					if (Zeny < 800) {
						mes "[Master]";
						mes "You know...";
						mes "I was going to charge you";
						mes "800 zeny for this drink, but";
						mes "it looks like you can't afford";
						mes "it right now. Come back when";
						mes "you have the zeny, alright?";
					}
					else {
						mes "[Master]";
						mes "Most bartenders would";
						mes "usually charge 1,000 zeny";
						mes "for this stuff, but I'll only";
						mes "ask you for 800. Enjoy it,";
						mes "my friend, and drink it as";
						mes "deeply as you would life.";
						set Zeny, Zeny-800;
						getitem 12112,1; //Tropical_Sograt
					}
					close;
				}
				else {
					if (Zeny < 1000) {
						mes "[Master]";
						mes "Hey, you don't have";
						mes "the zeny for this drink.";
						mes "Make sure you come back";
						mes "with 1,000 zeny if you want";
						mes "me to fix you something, okay?";
						close;
					}
					else {
						mes "[Master]";
						mes "For Assassins, I charge";
						mes "800 zeny, but for you, I'm";
						mes "gonna charge 1,000. No";
						mes "hard feelings, but you're";
						mes "already lucky to be here.";
						mes "Hey, enjoy your drink~";
						set Zeny, Zeny-1000;
						getitem 12112,1; //Tropical_Sograt
						close;
					}
				}
			}
		case 2:
			if (checkweight(908,200) == 0) {
				mes "[Master]";
				mes "Hey, why did you bring";
				mes "so much stuff with you?";
				mes "I can't give you anything";
				mes "to drink if you don't have";
				mes "the room to carry it around.";
				mes "Clear out your inventory!";
				close;
			}
			else {
				if (BaseClass == Job_Assassin) {
					if (Zeny < 800) {
						mes "[Master]";
						mes "You know...";
						mes "I was going to charge you";
						mes "800 zeny for this drink, but";
						mes "it looks like you can't afford";
						mes "it right now. Come back when";
						mes "you have the zeny, alright?";
					}
					else {
						mes "[Master]";
						mes "Most bartenders would";
						mes "usually charge 1,000 zeny";
						mes "for this stuff, but I'll only";
						mes "ask you for 800. Enjoy it,";
						mes "my friend, and drink it as";
						mes "deeply as you would life.";
						set Zeny, Zeny-800;
						getitem 12113,1; //Vermilion_The_Beach
					}
					close;
				}
				else {
					if (Zeny < 1000) {
						mes "[Master]";
						mes "Hey, you don't have";
						mes "the zeny for this drink.";
						mes "Make sure you come back";
						mes "with 1,000 zeny if you want";
						mes "me to fix you something, okay?";
						close;
					}
					else {
						mes "[Master]";
						mes "For Assassins, I charge";
						mes "800 zeny, but for you, I'm";
						mes "gonna charge 1,000. No";
						mes "hard feelings, but you're";
						mes "already lucky to be here.";
						mes "Hey, enjoy your drink~";
						set Zeny, Zeny-1000;
						getitem 12113,1; //Vermilion_The_Beach
						close;
					}
				}
			}
		case 3:
			mes "[Master]";
			mes "What are you...?";
			mes "I just told you";
			mes "I only serve two";
			mes "drinks. If you want";
			mes "water, you oughtta";
			mes "go someplace else.";
			close;
		}
	case 2:
		if (mao_request == 1) {
			if (BaseClass == Job_Assassin) {
				mes "[Master]";
				mes "A mission...";
				mes "Don't you think you're little too late?";
				mes "All the requests were cancelled and there's nothing left for you to do.";
				mes "Besides, first things first. The restoration work for the city is the most urgent.";
				close;
			}
			else {
				mes "[Master]";
				mes "Huh?";
				next;
				select("Show him the Letter of Recommendation.");
				if (countitem(7416) > 0) {
					mes "[Master]";
					mes "Ah.. I think you're little too late.";
					mes "I've got nothing to say if this's about the request sent to each guild.";
					mes "I'm sorry you've come this far for nothing.";
					close;
				}
				else {
					mes "[Master]";
					mes "What are you talking about?";
					next;
					mes "[Master]";
					mes "................You're looking way too suspicious now.";
					mes " ";
					mes "Boys, show this guy the door!!";
					next;
					mes "[Drinking Boys]";
					mes "- Yes, Sir!";
					next;
					mes "- You've got kicked out of the place. -";
					close2;
					warp "morocc",45,106;
					end;
				}
			}
		}
		else {
			if (BaseClass == Job_Assassin) {
				mes "[Master]";
				mes "Let me take a guess. You haven't been assigned any missions from this joint, eh?";
				mes "By the way, you like this place?";
				next;
				mes "[Master]";
				mes "You know, I kinda...";
				mes "happen to really love this little pub of mine..";
				mes "where some secret businesses take place..";
				next;
				mes "[Master]";
				mes "If you want, I'll recommend";
				mes "you to the guildmaster for";
				mes "a mission. Until then, you've";
				mes "got to focus on your training.";
				mes "And until you train... Why";
				mes "don't you enjoy a drink?";
				close;
			}
			else {
				mes "[Master]";
				mes "Mission...?";
				mes "Come on, what are";
				mes "you talking about?";
				mes "Stop talking crazy and";
				mes "order something to drink.";
				close;
			}
		}
	case 3:
		mes "[Master]";
		mes "Hmmm.";
		close;
	}
}

que_job01,49,49,5	script	Tao#dan_09	877,{
	if (BaseClass == Job_Assassin) {
		mes "[Tao]";
		mes "Welcome, meow~";
		mes "How's out, meow?";
		next;
		if (prt_curse == 24) {
			switch(select("Is Marjana in?:I'm here to see you,Tao.")) {
			case 1:
				mes "[Tao]";
				mes "She's in the room on your left, meow~";
				mes "But then, how d'you know she's here, meow?";
				close;
			case 2:
				mes "[Tao]";
				mes "This is no place for playing, meow..";
				mes "... Hey, it's awkward you looking at me like that, meow..";
				close;
			}
		}
		else {
			mes "[Tao]";
			mes "Is this all because of that dude called Raiyan Moore or More.. or.. blar?";
			mes "... Meow..";
			next;
			mes "[Tao]";
			mes "Ugh, you've got nothing to do, then get lost, meow?!";
			mes "You know this place is prohibited, meow.";
			mes "You won'e even be able to go into that room, anyway, meow~.";
			next;
			mes "[Tao]";
			mes "Or, else.... You could?.. Oh, who knows, meow..";
			mes "Tao hates to watch the gate anymore, meow~ meow~";
			close;
		}
	}
	else {
		mes "[Tao]";
		mes "What's up, meow!";
		next;
		switch(select("So, it's just you, little brat?:What has gotten into you?!:Hey, stop meowing?!")) {
		case 1:
			mes "[Tao]";
			mes "What are you talking about, meow!";
			mes "Tao's the greatest gate keeper!";
			mes "Don't ever low estimate, meow!";
			next;
			mes "[Tao]";
			mes "Looks like you came for some reasons, but don't even dream of getting my help, meow!!";
			mes "Do you think I'm stupid enough to tell you that";
			mes "the Master and Marjana are in that room on the right? Meow, meow, meow~~";
			close;
		case 2:
			mes "[Tao]";
			mes "Are you looking down on me because I'm a gate keeper?!";
			mes "Grrrr...";
			next;
			mes "[Tao]";
			mes "Get outta here while you have chances!!!!";
			close;
		case 3:
			mes "[Tao]";
			mes "Meow, meow.";
			mes "It's a long story, meow.";
			mes "That day, Tao really wanted a Big Ribbon, meow.";
			next;
			mes "[Tao]";
			mes "So I went out looking for Wild Roses, meow...";
			mes "...Meow? Wait a minute, why am I telling this story, meow?!";
			next;
			mes "[Tao]";
			mes "Krrrr!";
			mes "Who the hell are you!";
			next;
			mes "[Tao]";
			mes "Get out of here, meow!!!";
			close;
		}
	}
}

que_job01,51,55,0	script	que_job01#room_1	45,1,1,{

OnReset:
	set $@maobar_room,0;
	end;

OnTouch:
	if (prt_curse == 24) {
		if ($@maobar_room == 0) {
			set $@maobar_room,1;
			donpcevent " #room1timer::OnEnable";
			donpcevent "Marjana#poison::OnEnable";
			mes "[Tao]";
			mes "Ah, that place is protected";
			mes "by security magic, so you'll";
			mes "only have ^4D4DFF4 minutes^000000 to remain";
			mes "there. Don't waste time, meow!";
			close2;
			warp "que_job01",11,7;
			end;
		}
		else {
			mes "[Tao]";
			mes "Sooo sorry, meow~";
			mes "Someone else is already";
			mes "inside. Just come back";
			mes "again later, meow?";
			close2;
			warp "que_job01",52,50;
			end;
		}
	}
	else if (mao_request > 0) {
		if ($@maobar_room == 0) {
			set $@maobar_room,1;
			donpcevent " #room1timer::OnEnable";
			donpcevent "Valdes#moc_master_1::OnEnable";
			mes "[Tao]";
			mes "Ah, that place is protected";
			mes "by security magic, so you'll";
			mes "only have ^4D4DFF4 minutes^000000 to remain";
			mes "there. Don't waste time, meow!";
			close2;
			warp "que_job01",11,7;
			end;
		}
		else {
			mes "[Tao]";
			mes "Sooo sorry, meow~";
			mes "Someone else is already";
			mes "inside. Just come back";
			mes "again later, meow?";
			close2;
			warp "que_job01",52,50;
			end;
		}
	}
	else {
		mes "[Tao]";
		mes "Wh-who are you?";
		mes "This is a restricted";
		mes "area, meow! If you don't";
		mes "have permission, then get";
		mes "out of there right meow!";
		close2;
		warp "que_job01",52,50;
		end;
	}
}

que_job01,1,1,0	script	 #room1timer	844,{
OnEnable:
	initnpctimer;
	mapannounce "que_job01","You will now enter the Master Zone, Area 1.",bc_map,"0x70DBDB";
	end;

OnStop:
	mapannounce "que_job01","The security magic in the Master Zone, Area 1 is deactivated.",bc_map,"0x70DBDB";
	donpcevent "#room1_warp::OnDisable";
	donpcevent "Valdes#moc_master_1::OnDisable";
	donpcevent "Marjana#poison::OnDisable";
	donpcevent "que_job01#room_1::OnReset";
	stopnpctimer;
	end;

OnTimer240000:
	donpcevent "#room1_warp::OnEnable";
	end;

OnTimer245000:
	donpcevent "#room1_warp::OnDisable";
	donpcevent "Valdes#moc_master_1::OnDisable";
	donpcevent "Marjana#poison::OnDisable";
	end;

OnTimer250000:
	mapannounce "que_job01","The security magic Master Zone, Area 1 is now activated.",bc_map,"0x70DBDB";
	donpcevent "que_job01#room_1::OnReset";
	stopnpctimer;
	end;
}

que_job01,12,13,0	script	#room1_warp	-1,15,15,{
OnInit:
	disablenpc "#room1_warp";
	end;

OnEnable:
	enablenpc "#room1_warp";
	end;

OnDisable:
	disablenpc "#room1_warp";
	end;

OnTouch:
	warp "que_job01",52,50;
	end;
}

que_job01,51,44,0	warp	que_job01#room_2	1,1,que_job01,80,23

que_job01,11,4,0	script	que_job01#room1_out	45,1,1,{
OnTouch:
	warp "que_job01",52,50;
	donpcevent " #room1timer::OnStop";
	end;
}

que_job01,80,27,0	warp	que_job01#room2_1_out	1,1,que_job01,52,50

que_job01,16,21,5	script	Valdes#moc_master_1	55,{
	if (BaseClass == Job_Assassin) {
		mes "[Valdes]";
		mes "What is it? I have nothing to ask you to do.";
		mes "Could you please leave me alone? I have a bad headache.";
		next;
		mes "[Valdes]";
		mes "I'll ask you for a help some other time.";
		mes "You look pretty passionate after all.";
	}
	else {
		mes "[Valdez]";
		mes "....Sorry, but could you please leave?";
		mes "I'm really stressed out right now.";
		mes "If you're here for the request that we made earlier, you can just forget about it.";
	}
	if (countitem(7417) > 0) {
		delitem 7417,countitem(7417); //Mission_ScrollA
	}
	if (countitem(7418) > 0) {
		delitem 7418,countitem(7418); //Mission_ScrollB
	}
	if (countitem(7416) > 0) {
		delitem 7416,countitem(7416); //Letter_Of_Recommend
	}
	close;

OnInit:
	disablenpc "Valdes#moc_master_1";
	end;

OnEnable:
	enablenpc "Valdes#moc_master_1";
	end;

OnDisable:
	disablenpc "Valdes#moc_master_1";
	end;
}